@precedence { first @left, last @left }

@top Tree { featuresSection constraintsSection }

featuresSection {identifier lineEnd featureBlock }

AbstractItem { !first Feature abstract }

Feature { !last (identifier | string) RegExp*}
RegExp { (identifier | string) }
AbstractFeature { !last AbstractItem lineEnd stateBlock+}

abstract {"{abstract}"}

State{
  ("mandatory"
   | "optional"
   | "alternative"
   | "or")
}

constraintsSection {
    "constraints" lineEnd constraintsBlock+
}

ConstraintSign {
  "|" | "=>" | "&"
}

ConstraintsItem {
  Neg? Feature
}
Neg {
  "!"
}
Brackets {
 Neg? OpenBracket Constraints CloseBracket
}
OpenBracket {
  "("
}
CloseBracket {
  ")"
}

Constraints {
  (!last ConstraintsItem ConstraintSign ConstraintsItem) |
  (!first ConstraintsItem ConstraintSign Constraints) |
  (!first Constraints ConstraintSign ConstraintsItem) |
  (!first Constraints ConstraintSign Constraints) |
  (Brackets)
}

//!block

stateBlock { indent  (State lineEnd featureBlock)+ (dedent | eof) }
featureBlock { indent (Feature lineEnd | AbstractFeature)+ (dedent | eof) }
constraintsBlock {indent (Constraints lineEnd)+ (dedent | eof)}

//!skip

@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd

lineEnd { newline | eof }

//!context

@context trackIndent from "./tokens.js"

//!externalTokens

@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}

//!tokens

@tokens {
  @precedence {"mandatory", "optional", "alternative", "or", "constraints",identifier }
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  string { '"' (!["\\] | "\\" _)* '"' }
  identifier { $[a-zA-Z0-9_]+ }
}
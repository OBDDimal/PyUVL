@precedence { first @left, last @left, only @left}

@top Tree { includeSection? importSection? FeaturesSection ConstraintsSection? }

importSection { "imports" lineEnd ImportBlock}
includeSection { "include" lineEnd IncludeBlock}
include {
  "Boolean" |
  "Arithmetic" |
  "Type"
}

FeaturesSection { Root lineEnd FeatureBlock }
Root { "features" }

StateFeature { !last ExtendedFeature lineEnd StateBlock }
ImportFeature { ImportName ("."  Specifier)+}
ImportName { (identifier | string) }
Specifier { (identifier | string) }
Feature { (identifier | string) }
ExtendedFeature {!first Type? (Feature | ImportFeature) ReqExp? Cardinality? AttributeItem? }
Type { "Integer" | "String" | "Real" | "Boolean"}
AttributeItem {!first "{" list<AttributeSelection> "}"}
Cardinality {!last "cardinality " "[" Min ".." Max "]"}
Counter { !first "[" Min ".." Max "]" }
Min {number}
Max {number}

AttributeSelection { Key Value? }
Key { identifier | string | "'" identifier "'" }
Value { ("-"? number) | string | "'" identifier+ "'" | boolean | ("-"? real) | AttributeItem | identifier}
boolean { "true" | "false" }

//!utility
ReqExp { !last (identifier | string) }
list<item> { item ("," item)* }

State{
  ("mandatory"
   | "optional"
   | "alternative"
   | "or"
   | Counter)
}

//!constraints section
ConstraintsSection { "constraints" lineEnd ConstraintsBlock+ }

ConstraintSign { "|" | "=>" | "&" | "==" | "<=>" | "!="}
ConstraintsItem { Neg? ((identifier | string | id | extIdentifier) | ImportFeature) | BracketItem }
BracketItem { OpenBracket ConstraintsItem CloseBracket }
Neg { "!" }
Brackets { !first OpenBracket Constraints CloseBracket }
OpenBracket { "(" }
CloseBracket { ")" }
Number { ("-"? number) | (OpenBracket Number NumericOperator Number CloseBracket)}

Constraints {
  (!last Operation (Signs) Number (ConstraintSign ConstraintsItem)?) |
  (!first ConstraintsItem ConstraintSign ConstraintsItem) |
  (!last ConstraintsItem ConstraintSign Constraints) |
  (!first Constraints ConstraintSign (ConstraintsItem | Constraints | Number) ) |
  (!first (Constraints | ConstraintsItem) SymbolicOperator Number) |
  (!last ConstraintsItem SymbolicOperator ConstraintsItem Signs ConstraintsItem) |
  (!last ConstraintsItem ConstraintSign Number ) |
  (!last (ConstraintsItem | Constraints) NumericOperator (ConstraintsItem | Constraints) ) |
  (!only ConstraintsItem) |
  (!last (ConstraintsItem | Constraints) SymbolicOperator ConstraintsItem) |
  (Neg? Brackets)
}

Operation {!first (sum | avg | lenExpr)}
lenExpr {"len" "(" Key ")" }
sum {"sum" "(" Key ")"}
avg { "avg" "(" Key ")"}
SymbolicOperator { ">" | "<"}
NumericOperator { "+" | "-" | "*" | "/" }
Signs { "<" | ">" | "==" | "=<" | "<=" }

//!block
StateBlock { indent  (State lineEnd FeatureBlock)+ (dedent | eof) }
FeatureBlock { indent (ExtendedFeature lineEnd | StateFeature)+  (dedent | eof) }
ConstraintsBlock {indent (Constraints lineEnd)+ (dedent | eof)}
ImportBlock { indent (((Feature | ImportFeature) ("as" Feature)?) lineEnd)+  (dedent | eof) }
IncludeBlock { indent (include ("." (identifier | "*"))? lineEnd)+  (dedent | eof) }

//!skip
@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd
lineEnd { newline | eof }

//!context
@context trackIndent from "./tokens.js"

//!externalTokens
@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}
//!tokens
@tokens {
  @precedence {"mandatory", "optional", "alternative", "or", "constraints", "cardinality ","sum", "avg", "Integer", "Real","String", "Boolean","len", "-", "false", "true",real, number, identifier, extIdentifier, id}
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  string { ('"' (!["\\] | "\\" _)* '"')  }
  identifier { ("-"? $[a-zA-Z0-9_])+ }
  extIdentifier { "'" $[a-zA-Z0-9_]+ "'" }
  id { "'" "-"? $[0-9]+ "'" }
  number { $[0-9]+ }
  real { $[0-9]+ "." $[0-9]+}
}	
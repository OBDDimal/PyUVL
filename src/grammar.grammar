@precedence { first @left, last @left}

@top Tree { featuresSection constraintsSection? }

featuresSection {identifier lineEnd FeatureBlock }

StateFeature { ExtendedFeature lineEnd StateBlock }
Feature { (identifier | string) }
ExtendedFeature {ReqExp? Type? Feature ReqExp? Cardinality? AttributeItem?}
Type { "Integer" | "String" | "Real" | "Boolean"}
AttributeItem {!first "{" list<AttributeSelection> "}"}
Cardinality {!last "cardinality " "[" Min ".." Max "]"}
Min {number}
Max {number}

AttributeSelection { Key Value? }
Key { identifier | string }
Value { number | string | "'" identifier "'"}

//!utilty
ReqExp { !last (identifier | string) }
list<item> { item ("," item)* }

State{
  ("mandatory"
   | "optional"
   | "alternative"
   | "or")
}

//!constraints section
constraintsSection { "constraints" lineEnd ConstraintsBlock+ }

ConstraintSign { "|" | "=>" | "&" | "==" }
ConstraintsItem { Neg? (identifier | string) }
Neg { "!" }
Brackets { "(" Constraints ")" }
Number { number }

Constraints {
  (!last Operation Operator Number (ConstraintSign ConstraintsItem)?) |
  (!last ConstraintsItem ConstraintSign ConstraintsItem) |
  (!first ConstraintsItem ConstraintSign Constraints) |
  (!first Constraints ConstraintSign ConstraintsItem) |
  (!first Constraints ConstraintSign Constraints) |
  (Neg? Brackets)
}

Operation { sum | avg | lenExpr}
lenExpr {"len" "(" Attribute ")" }
sum {"sum" "(" Attribute ")"}
avg { "avg" "(" Attribute ")"}
Operator { "+" | "-" | "*" | "/" | "==" | ">" | ">=" }
Attribute { identifier | string }

//!block
StateBlock { indent  (State lineEnd FeatureBlock)+ (dedent | eof) }
FeatureBlock { indent (ExtendedFeature lineEnd | StateFeature)+  (dedent | eof) }
ConstraintsBlock {indent (Constraints lineEnd)+ (dedent | eof)}

//!skip
@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd
lineEnd { newline | eof }

//!context
@context trackIndent from "./tokens.js"

//!externalTokens
@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}
//!tokens
@tokens {
  @precedence {"mandatory", "optional", "alternative", "or", "constraints", "cardinality ","sum", "avg", "Integer", "Real","String", "Boolean","len", number, identifier}
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  string { '"' (!["\\] | "\\" _)* '"' }
  identifier { $[a-zA-Z0-9_]+ }
  number { $[0-9]+ }
}	
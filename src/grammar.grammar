@precedence { first @left, last @left}

@top Tree { importSection? featuresSection constraintsSection? }

importSection { "imports" lineEnd ImportBlock}

featuresSection { Root lineEnd FeatureBlock }
Root { "features" }

StateFeature { !last ExtendedFeature lineEnd (StateBlock | CounterBlock) }
ImportFeature { ImportName ("."  Specifier)+}
ImportName { (identifier | string) }
Specifier { (identifier | string) }
Feature { (identifier | string) }
ExtendedFeature {!first Type? (Feature | ImportFeature) ReqExp? Cardinality? AttributeItem? }
Type { "Integer" | "String" | "Real" | "Boolean"}
AttributeItem {!first "{" list<AttributeSelection> "}"}
Cardinality {!last "cardinality " "[" Min ".." Max "]"}
Counter { !first "[" Min ".." Max "]" }
Min {number}
Max {number}

AttributeSelection { Key Value? }
Key { identifier | string }
Value { number | string | "'" identifier "'" | boolean }
boolean { "true" | "false" }

//!utility
ReqExp { !last (identifier | string) }
list<item> { item ("," item)* }

State{
  ("mandatory"
   | "optional"
   | "alternative"
   | "or")
}

//!constraints section
constraintsSection { "constraints" lineEnd ConstraintsBlock+ }

ConstraintSign { "|" | "=>" | "&" | "==" | "<=>" }
ConstraintsItem { Neg? ((identifier | string) | ImportFeature) }
Neg { "!" }
Brackets { !first OpenBracket Constraints CloseBracket }
OpenBracket { "(" }
CloseBracket { ")" }
Number { number }

Constraints {
  (!last Operation Operator Number (ConstraintSign ConstraintsItem)?) |
  (!last ConstraintsItem ConstraintSign ConstraintsItem) |
  (!first ConstraintsItem ConstraintSign Constraints) |
  (!first Constraints ConstraintSign ConstraintsItem) |
  (!first Constraints ConstraintSign Constraints) |
  (!first ConstraintsItem Operator ConstraintsItem Signs ConstraintsItem) |
  (Neg? Brackets)
}

Operation { sum | avg | lenExpr}
lenExpr {"len" "(" Key ")" }
sum {"sum" "(" Key ")"}
avg { "avg" "(" Key ")"}
Operator { "+" | "-" | "*" | "/" | ">" | "<"}
Signs { "<" | ">" | "=="}

//!block
StateBlock { indent  (State lineEnd FeatureBlock)+ (dedent | eof) }
CounterBlock { indent  (Counter lineEnd FeatureListBlock)+ (dedent | eof) }
FeatureBlock { indent (ExtendedFeature lineEnd | StateFeature)+  (dedent | eof) }
FeatureListBlock { indent (ExtendedFeature lineEnd)+ (dedent | eof) }
ConstraintsBlock {indent (Constraints lineEnd)+ (dedent | eof)}
ImportBlock { indent (Feature lineEnd)+  (dedent | eof) }

//!skip
@skip {
  spaces |
  Comment |
  blankLineStart (spaces | Comment)* lineEnd
}

//!lineEnd
lineEnd { newline | eof }

//!context
@context trackIndent from "./tokens.js"

//!externalTokens
@external tokens indentation from "./tokens.js" {
  indent
  dedent
  blankLineStart
}
//!tokens
@tokens {
  @precedence {"mandatory", "optional", "alternative", "or", "constraints", "cardinality ","sum", "avg", "Integer", "Real","String", "Boolean","len", number, identifier}
  spaces { $[ \t]+ }
  newline { "\n" }
  eof { @eof }
  Comment { "#" ![\n]+ }
  string { '"' (!["\\] | "\\" _)* '"' }
  identifier { $[a-zA-Z0-9_]+ }
  number { $[0-9]+ }
}	